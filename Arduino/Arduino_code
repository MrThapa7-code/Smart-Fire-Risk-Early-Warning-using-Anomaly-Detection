#include <ArduinoBLE.h>
#include <DHT.h>
#include <Wire.h>
#include "rgb_lcd.h"

#define DHTPIN 2
#define DHTTYPE DHT11
#define FLAME_PIN 3
#define GAS_PIN A0
#define BUZZER_PIN 8
#define GAS_THRESHOLD 150

DHT dht(DHTPIN, DHTTYPE);
rgb_lcd lcd;

// BLE Service and Characteristic UUIDs
BLEService fireService("19B10000-E8F2-537E-4F6C-D104768A1216");
BLEStringCharacteristic dataChar("19B10001-E8F2-537E-4F6C-D104768A1216",
                                  BLERead | BLENotify, 50);

// LCD cycling
unsigned long lastLCDUpdate = 0;
int lcdPage = 0;

void setup() {
  Serial.begin(9600);

  pinMode(FLAME_PIN, INPUT);
  pinMode(BUZZER_PIN, OUTPUT);
  digitalWrite(BUZZER_PIN, LOW);

  dht.begin();

  // LCD Initialization
  lcd.begin(16,2);
  lcd.setRGB(0, 255, 0);
  lcd.setCursor(0,0);
  lcd.print("Fire/Gas Monitor");
  lcd.setCursor(0,1);
  lcd.print("Gas Thresh:150");

  // BLE Initialization
  if (!BLE.begin()) while(1);
  BLE.setLocalName("UNO_R4_FIRE");
  BLE.setAdvertisedService(fireService);
  fireService.addCharacteristic(dataChar);
  BLE.addService(fireService);
  BLE.advertise();
  Serial.println("BLE Device Ready: UNO_R4_FIRE");
}

void loop() {
  BLE.poll();

  int gasValue = analogRead(GAS_PIN);
  int rawFlame = digitalRead(FLAME_PIN);
  int flameDetected = (rawFlame == LOW) ? 1 : 0;
  bool gasDanger = (gasValue >= GAS_THRESHOLD);
  bool isDanger = gasDanger || flameDetected;

  digitalWrite(BUZZER_PIN, isDanger ? HIGH : LOW);

  float t = dht.readTemperature();
  float h = dht.readHumidity();
  if (isnan(t)) t = 0.0;
  if (isnan(h)) h = 0.0;

  // LCD display logic
  if (isDanger) {
    lcd.setRGB(255, 0, 0);
    lcd.setCursor(0,0);
    lcd.print(flameDetected ? "FLAME ALERT!    " : "GAS ALERT!      ");
    lcd.setCursor(0,1);
    lcd.print("G:");
    lcd.print(gasValue);
    lcd.print(" T:");
    lcd.print(t,1);
    lcd.print("      ");
  } else {
    if (millis() - lastLCDUpdate > 2000) {
      lastLCDUpdate = millis();
      lcd.setRGB(0, 255, 0);
      switch(lcdPage) {
        case 0:
          lcd.setCursor(0,0);
          lcd.print("G:");
          lcd.print(gasValue);
          lcd.print(" T:");
          lcd.print(t,1);
          lcd.print("    ");
          lcd.setCursor(0,1);
          lcd.print("H:");
          lcd.print(h,0);
          lcd.print("% Status: OK  ");
          break;
        case 1:
          lcd.setCursor(0,0);
          lcd.print("Flame:");
          lcd.print(flameDetected ? "YES" : "NO ");
          lcd.print("       ");
          lcd.setCursor(0,1);
          lcd.print("Monitoring...   ");
          break;
      }
      lcdPage = (lcdPage + 1) % 2;
    }
  }

  // Serial debug
  static unsigned long lastDebug = 0;
  if (millis() - lastDebug > 500) {
    lastDebug = millis();
    Serial.print("GAS: "); Serial.print(gasValue);
    Serial.print(" | FLAME: "); Serial.println(rawFlame);
    if (gasDanger) Serial.println("-> GAS ALERT!");
    if (flameDetected) Serial.println("-> FLAME DETECTED!");
  }

  // ---------------- BLE send ----------------
  static unsigned long lastSent = 0;
  if (millis() - lastSent > 1000) { // send every 1 second
    lastSent = millis();

    String status = isDanger ? "ALERT" : "OK";
    String tStr = String(t,1);
    String hStr = String(h,0);
    String payload = status + "|" + tStr + "|" + String(gasValue) + "|" + hStr + "|" + String(flameDetected);

    if (BLE.connected()) { // only send if a central is connected
        dataChar.writeValue(payload);  // automatically notifies central
    }

    Serial.println("BLE Sent: " + payload); // debug print
  }
}
